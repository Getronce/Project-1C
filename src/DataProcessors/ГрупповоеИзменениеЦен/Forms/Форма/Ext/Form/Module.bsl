&НаКлиенте
Процедура ИзменитьЦены(Команда)
	Если ПроверитьЗаполнение() Тогда
		ИзменитьЦеныНаСервере();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЦеныНаСервере()
    
    ТаблицаЦен = ПолучитьАктуальныеЦены();
    
    Если ТаблицаЦен.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "По указанной группе " + ГруппаНоменклатур + " и Виду цен " + ВидЦены + " не найдено установленных цен!";
        Сообщение.Сообщить();
        Возврат;
    КонецЕсли;
    
    СоздатьДокументУстановкиЦен(ТаблицаЦен);
    
КонецПроцедуры

&НаСервере
Функция ПолучитьАктуальныеЦены()
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
    |	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
    |ИЗ
    |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
    |ГДЕ
    |	ЦеныНоменклатурыСрезПоследних.ВидЦены = &ВидЦены
    |	И ЦеныНоменклатурыСрезПоследних.Номенклатура В ИЕРАРХИИ(&ГруппаНоменклатур)";
    
    Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
    Запрос.УстановитьПараметр("ГруппаНоменклатур", ГруппаНоменклатур);
    
    Результат = Запрос.Выполнить();
    Возврат Результат.Выгрузить();
    
КонецФункции

&НаСервере
Процедура СоздатьДокументУстановкиЦен(ТаблицаЦен)
    
    НовыйДокумент = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
    
    НовыйДокумент.Дата = ДатаДействияНовойЦены;
    НовыйДокумент.Комментарий = "Изменение цен на " + ПроцентИзменения + "%. Документ создан обработкой группового изменений цен";
    
    ТабличнаяЧасть = НовыйДокумент.Товары;
    
    Для Каждого СтрокаЦены Из ТаблицаЦен Цикл
        НоваяСтрока = ТабличнаяЧасть.Добавить();
        НоваяСтрока.Номенклатура = СтрокаЦены.Номенклатура;
		НоваяСтрока.ВидЦены = ВидЦены;
        НоваяСтрока.Цена = СтрокаЦены.Цена * (1 + ПроцентИзменения / 100);
    КонецЦикла;
    
    НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	ДокументУстановкиЦен = НовыйДокумент.Ссылка;
    
КонецПроцедуры